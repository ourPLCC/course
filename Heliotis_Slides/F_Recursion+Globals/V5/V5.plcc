# Language V4: procs

skip WHITESPACE '\s+'
skip COMMENT '%.*'

ADD1OP 'add1'
ADDOP '\+'
ASSIGN '='
COMMA ','
DIVOP '/'
DOT '\.'
ELSE 'else'
IF 'if'
IN 'in'
LBRACE '\{'
LET 'let'
LETREC 'letrec'
LPAREN '\('
MULOP '\*'
PROC 'proc'
RBRACE '\}'
RPAREN '\)'
SEMI ';'
SUB1OP 'sub1'
SUBOP '\-'
THEN 'then'
ZEROP 'zero\?'

LIT '\d+'
VAR '[A-Za-z]\w*\??'
%
<program>        ::= <exp>
<exp>:AppExp     ::= DOT <exp> LPAREN <operands> RPAREN
<exp>:IfExp      ::= IF <exp>test THEN <exp>thenPart ELSE <exp>elsePart
<exp>:LetExp     ::= LET <letDecls> IN <exp>
<exp>:LetrecExp  ::= LETREC <letrecDecls> IN <exp>
<exp>:LitExp     ::= <LIT>
<exp>:PrimAppExp ::= <prim> LPAREN <operands> RPAREN
<exp>:ProcExp    ::= PROC LPAREN <formals> RPAREN <exp>
<exp>:SeqExp     ::= LBRACE <exp> <seqExps> RBRACE
<exp>:VarExp     ::= <VAR>
<letDecls>       **= <VAR> ASSIGN <exp>
<letrecDecls>    **= <VAR> ASSIGN <exp>
<operands>       **= <exp> +COMMA
<seqExps>        **= SEMI <exp>

<prim>:AddPrim   ::= ADDOP
<prim>:SubPrim   ::= SUBOP
<prim>:Add1Prim  ::= ADD1OP
<prim>:Sub1Prim  ::= SUB1OP
<prim>:MulPrim   ::= MULOP
<prim>:DivPrim   ::= DIVOP
<prim>:ZeropPrim ::= ZEROP

<formals>        **= <VAR> +COMMA
%

# class methods that just print out the expression
# (Remember that use of toString is only mandated at the tree's
#  root node.)

include Add1Prim-i.java
include AddPrim-i.java
include AppExp-i.java
include DivPrim-i.java
include Exp-i.java
include Formals-i.java
include IfExp-i.java
include LetDecls-i.java
include LetExp-i.java
include LetrecDecls-i.java
include LetrecExp-i.java
include LitExp-i.java
include MulPrim-i.java
include Operands-i.java
include Prim-i.java
include PrimAppExp-i.java
# include Proc-i.java
include ProcExp-i.java
include Program-i.java
include SeqExp-i.java
include Sub1Prim-i.java
include SubPrim-i.java
include VarExp-i.java
include ZeropPrim-i.java

include EnvSource/Binding-i.java
include EnvSource/Bindings-i.java
include EnvSource/Env-i.java
include EnvSource/EnvNode-i.java
include EnvSource/IntVal-i.java
include EnvSource/ProcVal-i.java
include EnvSource/Val-i.java

